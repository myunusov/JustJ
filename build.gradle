apply plugin: "idea"
apply plugin: "eclipse"

description = 'JustJ is a Light POJO IoC and ORM Framework'
version = '1.0.1-SNAPSHOT'

allprojects {
  group = 'org.maxur.justj'
}

subprojects {
  repositories {
    if (project.hasProperty("mavenProxyUrl")) {
      maven {
        url = mavenProxyUrl
      }
    } else {
      mavenLocal()
      mavenCentral()
    }
  }
}

configure(subprojects.findAll {!it.name.contains('sample')}) {
  apply plugin: 'maven'

  uploadArchives {
    repositories.mavenDeployer {
      configuration = configurations.archives

      if (project.hasProperty("deployRepositoriesUrl")) {
        repository(url: deployRepositoriesUrl + "/releases/") {
          authentication(userName: deployRepositoriesUser, password: deployRepositoriesPassword)
        }
        snapshotRepository(url: deployRepositoriesUrl + "/snapshots/") {
          authentication(userName: deployRepositoriesUser, password: deployRepositoriesPassword)
        }
      }

      pom.project {
        packaging 'pom'
        url 'https://github.com/myunusov/JustJ.git'
        inceptionYear '2014'

        scm {
          url 'scm:git@github.com:myunusov/JustJ.git'
          connection 'scm:git@github.com:myunusov/JustJ.git'
        }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'myunusov'
            name 'Maxim Yunusov'
            email 'myunusov@maxur.org'
            url 'http://www.maxur.org'
            roles {
              role 'Developer'
            }
            timezone '-6'
          }
        }
      }
    }
  }

}

configure(subprojects.findAll {it.name != 'sample'}) {
  apply plugin: "java"
  apply plugin: "sonar-runner"

  println('MODULE: ' + name)

  ext.slf4j_version='1.7.7'

  dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4j_version

    compile 'edu.washington.cs.types.checker:checker-framework:1.7.5'

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
  }

  sonarRunner {
    sonarProperties {
      property "sonar.host.url", sonarUrl
      property "sonar.jdbc.url", sonarJdbcUrl
      property "sonar.jdbc.driverClassName", sonarJdbcDriverClassName
      property "sonar.jdbc.username", sonarJdbcUsername
      property "sonar.jdbc.password", sonarJdbcPassword
      property "sonar.sourceEncoding", "UTF-8"
      property "sonar.language", "java"
    }
  }

  if (project.hasProperty("withJaCoCo")) {

    println("With JaCoCo")

    apply plugin: 'jacoco'

    sonarRunner {
      sonarProperties {
        property "sonar.jacoco.reportPath", "$buildDir/jacoco/jacocoTest.exec"
        property "sonar.jacoco.itReportPath", "$buildDir/jacoco/jacocoIntegTest.exec"
      }
    }

    sourceSets {
      integtest {
        java {
          compileClasspath += main.output
          runtimeClasspath += main.output
          srcDir 'src/integtest/java'
        }
      }
    }

    configurations {
      integtestCompile.extendsFrom testCompile
    }

    task "integtest"(type: Test, dependsOn: integtestClasses) {
      testClassesDir = sourceSets.integtest.output.classesDir
      classpath = sourceSets.integtest.runtimeClasspath

      jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoIntegTest.exec")
      }
    }

    test {
      jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
      }
    }
  }

  tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  task jars << {
    configurations.compile.each { File file -> println file.name }
  }

// custom tasks for creating source/javadoc jars
  task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

// add javadoc/source jar tasks as artifacts
  artifacts {
    archives sourcesJar
    archives javadocJar
  }

}


task wrapper(type: Wrapper) {
  gradleVersion = '2.0'
}